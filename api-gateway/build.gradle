plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id("com.google.cloud.tools.jib") version "3.4.2"
}

group = 'club.gach-dong'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.3")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway'

	//Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.5.0'

	// JWT
	def jjwtVersion = "0.11.5"
	implementation "io.jsonwebtoken:jjwt-api:$jjwtVersion"
	runtimeOnly "io.jsonwebtoken:jjwt-impl:$jjwtVersion"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:$jjwtVersion"

	// metrics
//	implementation "io.micrometer:micrometer-tracing-bridge-otel:1.3.2"
//	implementation "io.opentelemetry:opentelemetry-exporter-zipkin:1.40.0"
	implementation "io.micrometer:micrometer-registry-prometheus:1.13.2"


	// lombok slf4j
	compileOnly("org.projectlombok:lombok")
	annotationProcessor("org.projectlombok:lombok")

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

jib {
	def activeProfile = System.getenv("SPRING_PROFILES_ACTIVE")
	def imageName = System.getenv("IMAGE_NAME")
	def imageTag = System.getenv("IMAGE_TAG")
	def serverPort = System.getenv("SERVER_PORT") ?: "8080"

	from {
		image = "amazoncorretto:17-alpine3.17-jdk"
	}
	to {
		image = imageName
		tags = [imageTag, "latest"]
	}

	container {
		jvmFlags = [
				"-Dspring.profiles.active=${activeProfile}",
				"-Dserver.port=${serverPort}",
				"-Djava.security.egd=file:/dev/./urandom",
				"-Dfile.encoding=UTF-8",
				"-Duser.timezone=Asia/Seoul",
				"-XX:+UnlockExperimentalVMOptions",
				"-XX:+UseContainerSupport",
				"-XX:+UseG1GC",  // 필요 시 UseSerialGC로 변경 가능
				"-XX:InitialHeapSize=256m",  // 힙 사이즈를 더 낮게 설정
				"-XX:MaxHeapSize=512m",      // 최대 힙 사이즈도 조정
				"-XX:+DisableExplicitGC",
				"-server"
		]
		ports = [serverPort]
	}
}
